plugins {
  id "groovy"
  id "maven-publish"
	id "signing"

  id "com.github.jk1.dependency-license-report" version "1.3"
  id "com.gradle.plugin-publish" version "0.11.0"
	id "java-gradle-plugin"

	id "net.saliman.properties" version "1.5.1"
	id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

group = "com.marklogic"
version = "4.2.0"

sourceCompatibility = "8"
targetCompatibility = "8"

repositories {
  mavenLocal() // Used only for local development

	mavenCentral()

	// Needed for marklogic-unit-test-client
  jcenter()
}

dependencies {
  implementation gradleApi()
	implementation localGroovy()
	api "com.marklogic:ml-app-deployer:4.2.0"
	implementation "com.marklogic:mlcp-util:0.9.0"
	implementation "com.marklogic:marklogic-data-movement-components:2.3.0"
	implementation "commons-io:commons-io:2.8.0"

	compileOnly "com.marklogic:marklogic-unit-test-client:1.0.0"

	testImplementation localGroovy()
	testImplementation gradleTestKit()
	testImplementation 'xmlunit:xmlunit:1.3'

	// Force spock-core to use 4.13.1 to avoid security issue in 4.11
	testImplementation 'junit:junit:4.13.1'
	testImplementation('org.spockframework:spock-core:1.1-groovy-2.4') {
		exclude module: 'groovy-all'
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier 'sources'
  from sourceSets.main.allJava
  from sourceSets.main.allGroovy
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier "javadoc"
	from javadoc
}
javadoc.failOnError = false

artifacts {
	archives javadocJar, sourcesJar
}
signing {
	sign configurations.archives
}

publishing {
	publications {
		mainJava(MavenPublication) {
			pom {
				name = "${group}:${project.name}"
				description = "Gradle plugin for automating everything involving MarkLogic"
				packaging = "jar"
				url = "https://github.com/marklogic-community/${project.name}"
				licenses {
					license {
						name = "The Apache License, Version 2.0"
						url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
					}
				}
				developers {
					developer {
						id = "marklogic"
						name = "MarkLogic Github Contributors"
						email = "general@developer.marklogic.com"
						organization = "MarkLogic"
						organizationUrl = "https://www.marklogic.com"
					}
				}
				scm {
					url = "git@github.com:marklogic-community/${project.name}.git"
					connection = "scm:git@github.com:marklogic-community/${project.name}.git"
					developerConnection = "scm:git@github.com:marklogic-community/${project.name}.git"
				}
			}
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
	repositories {
		maven {
			name = "central"
			url = mavenCentralUrl
			credentials {
				username mavenCentralUsername
				password mavenCentralPassword
			}
		}
	}
}

pluginBundle {
  website = 'https://www.marklogic.com/'
  vcsUrl = 'https://github.com/marklogic-community/' + project.name + '.git'
  description = 'Publishes ml-gradle'
  tags = ['marklogic']

  plugins {
    mlgradlePlugin {
      id = 'com.marklogic.ml-gradle'
      displayName = 'ml-gradle for MarkLogic'
      description = 'Gradle plugin for configuring and deploying applications to MarkLogic'
      tags = ['marklogic']
      version = project.version
    }
  }

  mavenCoordinates {
    version = project.version
  }
}

// See https://github.com/snyk/gradle-plugin for more information
snyk {
	//arguments = '--all-sub-projects'
	severity = 'low'
	api = snykToken
	autoDownload = true
	autoUpdate = true
}
