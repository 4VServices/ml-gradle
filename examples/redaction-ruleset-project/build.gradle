buildscript {
	repositories {
		jcenter()
		// Needed for corb dependency: XCC
		maven { url "http://developer.marklogic.com/maven2/" }
	}
	dependencies {
		//Needed for CorbTask to dynamicaly generate properties from CORB Options class
		classpath 'com.marklogic:marklogic-corb:2.3.2'
	}
}

plugins {
	id "com.marklogic.ml-gradle" version "3.0.0"
}

repositories {
	jcenter()

	// Needed for corb dependency: XCC
	maven { url "http://developer.marklogic.com/maven2/" }
}

configurations {
	// This configuration captures the dependencies for running corb (Content Reprocessing in Bulk).
	// This is only needed if you want to run corb via Gradle tasks.
	// If you do, using com.marklogic.gradle.task.CorbTask is a useful starting point, as shown below.
	corb
}

dependencies {
	// required to run CoRB2
	corb 'com.marklogic:marklogic-corb:2.3.2'
	// optional
	//corb 'org.jasypt:jasypt:1.9.2' // would be necessary to leverage JasyptDecrypter
}

/*
 * ml-gradle adds an instance of com.marklogic.appdeployer.AppConfig to the Gradle project under the key "mlAppConfig".
 * This instance can be modified to affect the behavior of ml-gradle.
 */
ext {
	// mlAppConfig is an instance of com.marklogic.appdeployer.AppConfig
	mlAppConfig {
		// XCC URL for running corb task below and for creating triggers on pre-8.0-4 builds of MarkLogic
		contentXccUrl = "xcc://${mlUsername}:${mlPassword}@${mlHost}:${mlRestPort}"
	}
}

/*
 * This optional task demonstrates validating redaction rules via a locally run CORB process.
 * To execute this task simply run it after you have successfully deployed the redaction rulesets (via mlAppDeploy or mlLoadSchemas).
 */
task validateRedactionRulesetsAdhoc(type: com.marklogic.gradle.task.CorbTask) {
	xccConnectionUri = contentXccUrl
	urisModule = "src/main/ml-modules/ext/redaction-ruleset-validation/corb/redaction-rules-uris.xqy|ADHOC"
	processModule = "src/main/ml-modules/ext/redaction-ruleset-validation/corb/validate-redaction-rules.xqy|ADHOC"

}

